;(use parameterset TROSYF3GPPHSI19.2)
;
;M. Czisch & R. Boelens, J. Magn. Reson. 134, 158-160 (1998)
;K. Pervushin, G. Wider & K. Wuethrich, J. Biomol. NMR 12, 
;   345-348 (1998)
;A. Meissner, T. Schulte-Herbrueggen, J. Briand & O.W. Sorensen, Mol. Phys. 96,
;   1137-1142 (1998)
;J. Weigelt, J. Am. Chem. Soc. 120, 10778-10779 (1998)
;M. Rance, J.P. Loria & A.G. Palmer III, J. Magn. Reson. 136, 91-101 (1999)
;G. Zhu, X.M. Kong & K.H. Sze, J. Biomol. NMR 13, 77-81 (1999)
;
;$CLASS=HighRes
;$DIM=2D
;$TYPE=
;$SUBTYPE=
;$COMMENT=

prosol relations=<triple>

#include <Avance.incl>
#include <Grad.incl>
#include <Delay.incl>

; change the  p61 and p62 to 50 or 60 to get more water selectivity. 
"p61 = 40u"
"p63 = 40u"

"p62 = p21"
"plw7 = 0"
"plw0 = 0"
 

"spw39=plw1*pow(p1/p61, 2)"
"p39 =p61*37.248"    
"spoff39=0.0"     
;spnam39  295_rf0.00_37.248p1_bw1.04p1.mrf;

"d39 = p39*0.75"

"p31 =p1*59"     
"spoff31=0.0"    
"spw31=plw1"     
;spnam31 s0.49csH180x0.49cs_rf0.00_59.000p1_bw0.44p1.mrf


"spw37=plw1*pow(p1/p63, 2)"
"p37 =p63*27.125"   
"spoff37=0.0"     
;spnam37  0.5csNH90x0.5csAz2z_rf0.00_27.125p1_bw0.91p1.mrf;


"p14 =p3*10.021"     
"spoff3=0.0"     
"spw14=plw2"     
;spnam14 z2iz_rf0.00_10.021p1_bw2.40B1.mrf;


"spw51=plw3"
"p51 =p21*22"    
"spoff51=0.0"     
;spnam51  0.49csN180x0.49cs_rf0.00_22.000p1_bw0.25p1.mrf; 

"spw52=plw3"
"p52 =p62*22"    
"spoff52=0.0"    
;spnam52 0.49csN180x0.49cs_rf0.00_22.000p1_bw0.25p1.mrf; 

"spw54=plw3*pow(p21/p62, 2)"
"p54 =p62*7.093"    
"spoff54=0.0"    
;spnam54 pix_rf0.00_7.093p1_bw0.80B1.mrf

"p59 = p37/13.86"
"spw45=plw3*pow(p21/p59, 2)"
"p45 =p59*13.86"    
"spoff45=0.0"      
;spnam45 piby2x_rf0.00_13.860p1_bw2.00B1.mrf 


"p2=p1*2"
"p22=p21*2"
"d11=30m"
"d26=1s/(cnst4*4)"


"d0=6u"

"in0 = inf1/2"
"d51 = p39/4"
"d52 = p52/2"

"DELTA1=d26-p16-d16-4u-p37/2 -0.49*p51"
"DELTA2=d26-p16-d16-8u - p37/2-0.49*p52"
"DELTA3=d26-p16-d16-4u-d39 -p37/2" 
"DELTA4=d19-p22/2"
"DELTA5=d26-p16-d16-p21-4u-d39 " 

#   ifdef LABEL_CN
"DELTA6=p14+d0*2+p21*4/3.1416+4u"
#   else
"DELTA6=d0*2+p21*4/3.1416+2u"
#   endif /*LABEL_CN*/

"d37 = p37/2"

;"DELTA6=0"
"d37 = d37-DELTA6"
"DELTA5 = DELTA5-DELTA3"


"l0=1"


1 ze 
2 d11
3 d1 pl0:f1
  50u UNBLKGRAD
  (p37:sp37 ph1);(p1 ph1)
  4u
  p16:gp1
  d16
  DELTA1 pl7:f3 pl0:f1
  (center (p31:sp31 ph11) (p51:sp51 ph1):f3 ) 
  4u 
  DELTA1 pl3:f3 pl0:f1
  p16:gp1
  d16
  (p37:sp37 ph8);(p1 ph8) 
  4u pl0:f1
  
  p16:gp1
  d16 pl1:f1

  if "l0 %2 == 1"
     {
     (p21 ph3):f3
     }
  else
     {
     (p21 ph4):f3
     }


#   ifdef LABEL_CN
  d0 gron0
  2u groff
  (p14:sp14 ph1):f2
  d0 gron0*-1
  2u groff
#   else
  d0 gron0
  d0 gron0*-1
  2u groff pl7:f3
#   endif /*LABEL_CN*/
d37 pl0:f1 pl7:f3
 (p54:sp54 ph1):f3
(p37:sp37 ph5)
  p16:gp2
  d16
  DELTA2 pl0:f1  pl7:f3
  (center (p31:sp31 ph21)  (p52:sp52 ph1):f3) 
  p16:gp2
  d16
  DELTA2 pl0:f1  pl3:f3

  4u
  4u pl0:f1  pl3:f3
  (center (p37:sp37 ph1) (p21 ph2):f3 )  

  4u
  p16:gp3
  d16 pl0:f1  pl7:f3 
  (center (p39:sp39 ph1 d39):f1  (p52:sp52 ph1):f3) 

  DELTA5
  p16:gp3
  d16  pl18:f1  pl3:f3
  4u BLKGRAD

  (p21 ph7):f3
  go=2 ph31
  d11 mc #0 to 2 F1EA(calph(ph5, +180) & calph(ph7, +180) & calclc(l0, 1), caldel(d0, +in0) & calph(ph3, +180) & calph(ph4, +180) & calph(ph31, +180))
exit 
  

ph1=0
ph2=1
ph3=1 3 2 0
ph4=3 1 2 0
ph5=3
ph6=2
ph7=2
ph8=3
ph31=0 2 3 1

ph21=0
ph22=1
ph10=0;
ph11=1
ph12=2
ph13=3

 
;p61 and p63 : use 60 for higher water selectivity. ie., to observe peaks near water
;d0 : incremented delay (2D)                         [6 usec]
;d1 : relaxation delay; 1-5 * T1
;d11: delay for disk I/O                             [30 msec]
;d16: delay for homospoil/gradient recovery
;d19: delay for binomial water suppression 
;d26 : 1/(4J)YH
;cnst4: = J(YH)
;inf1: 1/SW(X) = 2 * DW(X)
;in0: 1/(2 * SW(X)) = DW(X)
;nd0: 2
;NS: 4 * n
;DS: 16
;td1: number of experiments
;FnMODE: echo-antiecho


;use gradient ratio:    gp 0 : gp 1 : gp 2 : gp 3
;                          3 :   30 :   45 :   50

;for z-only gradients:
;gpz0: 3%
;gpz1: 30%
;gpz2: 45%
;gpz3: 50%

;use gradient files:
;gpnam1: SMSQ10.100
;gpnam2: SMSQ10.100
;gpnam3: SMSQ10.100

 